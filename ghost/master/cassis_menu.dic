//******************************************************************************
// YAYA　ゴーストテンプレート
// メニュー処理辞書
//******************************************************************************

//******************************************************************************
//メインメニューの展開
//
//AYATEMPLATE.MenuItemの使い方
//1つめの文字列＝選択肢に表示する文字列
//2つめの文字列＝選んだときに実行される関数名
//3つめの文字列＝改行する量　50%刻み　省略すると100%（＝\n）
//******************************************************************************

//------------------------------------------------------------------------------
//メインメニュー展開
//------------------------------------------------------------------------------
OpenMenu
{
	"\1\s[10]\0\s[0]"
	--
	/*
		シェルによってメニューを分岐させる。
		座り表示非対応シェルの場合は座り関係のメニューを表示しない。
	*/
	if strMenuMode != "new" {
		"\b[2]"
	}
	--
	"お品書き、ね。"
	"何?"
	"何かしら?"
	"ん?"
	"\s[6]ふふ。"
	"どうしたの?"
	"どうしたいの?"
	"?"
	"何がしたいの?"
	--
	"\_q"
	--
	AYATEMPLATE.MenuItem("トーク（t）","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("もう一度（l）","Menu_LASTTALK")
	}
	--
	if GetShellnameEx == "Gemini"
	{
		AYATEMPLATE.MenuItem("二人で話す","Menu_GEMINITALK")
	}
	--
	// クリスマスケーキ
	if flgGetCake == 1
	{
		AYATEMPLATE.MenuItem("ケーキを食べる","comChristmasCakes")
		--
		"\n[half]"
	}
	--
	AYATEMPLATE.MenuItem("コミュニケート（c）","Menu_COMMUNICATEOPEN")
	--
	if "オフィーリア" _in_ ghostexlist || "リーライナ" _in_ ghostexlist {
		AYATEMPLATE.MenuItem("他のゴーストに話しかける", "StartCommunicate")
	}
	--
	AYATEMPLATE.MenuItem("コマンド（d）", "Menu_COMMAND")
	--
	// 表示設定がnewでなければ従来通り表示
	if strMenuMode != "new"
	{
		Menu_Shell
	}
	--
	AYATEMPLATE.MenuItem("設定","Menu_CONFIG")
	--
	AYATEMPLATE.MenuItem("連絡","Menu_WEBCLAP")
	--
	"\n[half]"
	--
	AYATEMPLATE.MenuItem("メニューを閉じる","Menu_CANCEL")
}

// コミュニケート
Menu_COMMUNICATEOPEN
{
	//---- コミュニケートボックスオープン
	"\1\s[10]\0\s[0]何かしら?"
	"\1\s[10]\0\s[0]はいはい。"
	"\1\s[10]\0\s[0]ん?"
	--
	"\![open,communicatebox]\e"
}

//**** メニュー項目選択 *****************************************************************

//---- メインメニュー
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

TALKINTERVAL {
	//---- トーク頻度の調整
	"\1\s[10]\0\s[0]どのくらいがいいの?"
	--
	AYATEMPLATE.MenuItem("うるさいくらい", "TALKMAX", 200)
	--
	AYATEMPLATE.MenuItem("ふつう", "TALKNORMAL")
	--
	AYATEMPLATE.MenuItem("静か", "TALKPOOR")
	--
	AYATEMPLATE.MenuItem("黙ってて", "TALKNONE")
	--
	AYATEMPLATE.MenuItem("戻る", "OpenMenu", 150)
}

//------------------------------------------------------------------------------

// Web拍手
Menu_WEBCLAP {
	"\1\s[10]\0\s[0]?\w9\n/
	\![open,inputbox,OnInputWebclap,0,バグ報告などどうぞ（最大200字）]\e"
}
Menu_CANCEL {
	//---- キャンセルされた
	"\1\s[10]\0\s[6]そう。\e"
}

//---- トーク頻度
Menu_TALKMAX {
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\1\s[10]\0\s[0]気に障るようなら言ってちょうだい。\e"
}

Menu_TALKNORMAL {
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\1\s[10]\0\s[0]普通というのも曖昧ね。\e"
}

Menu_TALKPOOR {
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\1\s[10]\0\s[0]静か、ね。\e"
}

Menu_TALKNONE {
	//---- 話さない
	aitalkinterval = 0
	"\1\s[10]\0\s[0]わかったわ。\w9\n本でも読んでようかしら。\e"
	"\1\s[10]\0\s[6]そう、\w4静かにしているわ。\e"
}

//---- ヘッドライン
Menu_HEADLINECANCEL {
	//---- キャンセルされた
	"\1\s[10]\0\s[0]そう。\e"
}

//---- トークウェイト設定
Menu_WCC {
	"\1\s[10]\0\s[0]\q[早く,WCFAST]\n/
	\q[普通,WCNORMAL]\n/
	\q[遅く,WCSLOW]\e"
}

Menu_WCFAST {
	WC = "fast"
	"\1\s[10]\0\s[6]噛まない程度に早くするわ。\w9\e"
}

Menu_WCNORMAL {
	WC = "normal"
	"\1\s[10]\0\s[6]そう。\w9\e"
}

Menu_WCSLOW {
	WC = "slow"
	"\1\s[10]\0\s[6]わかったわ。\w9\e"
}

//---- 設定
Menu_CONFIG {
	if strMenuMode == "new" {
		"\b[2]"
	}
	--
	"\1\s[10]\0\s[0]どうしたいの?"
	--
	AYATEMPLATE.MenuItem("トーク頻度", "TALKINTERVAL", 200)
	--
	AYATEMPLATE.MenuItem("トーク速度", "WCC")
	--
	if shellname == "master" {
		if flgTooltip != "1" {
			AYATEMPLATE.MenuItem("ツールチップを表示する", "Menu_TooltipEnable")
		}
		else {
			"\![*]\q[ツールチップを表示しない,Menu_TooltipDisable]\n"
		}
	}
	--
	if strMenuMode == "new" && flgNoSfcnv != 1
	{
		Menu_Shell
	}
	--
	if flgNoSfcnv != 1
	{
		if strMenuMode == "new" {
			"\![*]\q[着せ替え・座る／立つメニューをメニューに,Menu_Classic]\n"
		}
		else {
			"\![*]\q[着せ替え・座る／立つメニューを設定に,Menu_New]\n"
		}
	}
	--
	"\![*]\q[アンインストール設定,Menu_VANISHCONTROL]\n"
	--
	// 見切れ設定
	if flgMikireEnable == "1" {
		"\![*]\q[見切れトークを無効にする,Menu_MikireDisable]\n"
	}
	else {
		"\![*]\q[見切れトークを有効にする,Menu_MikireEnable]\n"
	}
	--
	"[half]"
	--
	"\n\n[half]\q[メニューへ戻る,OpenMenu]\n/
	\q[キャンセル,CANCEL]\e"
}

// ツールチップ制御
Menu_TooltipEnable {
	flgTooltip = 1
	"\1\s[10]\0\s[6]わかったわ。\e"
}

Menu_TooltipDisable {
	flgTooltip = 0
	"\1\s[10]\0\s[6]そう。\e"
}

// 見切れトーク制御
Menu_MikireEnable {
	flgMikireEnable = "1"
	"\1\s[10]\0\s[6]\e"
}

Menu_MikireDisable {
	flgMikireEnable = "0"
	"\1\s[10]\0\s[0]\e"
}


// メニュー設定
Menu_Classic {
	strMenuMode = "classic"
	"\1\s[10]\0\s[0]こうかしら?\e"
}

Menu_New {
	strMenuMode = "new"
	"\1\s[10]\0\s[0]こう?\e"
}

// アンインストールメニュー設定
Menu_VANISHCONTROL {
	"（アンインストールメニューの表示設定です。）\n/
	（現在:"
	--
	if vanishflg != 1 {
		"非表示)\n/
		\![*]\q[アンインストールメニュー表示,Menu_VANISH_TRUE]"
	} else {
		"表示)\n/
		\![*]\q[アンインストールメニュー非表示,Menu_VANISH_FALSE]"
	}
	--
	"\n\n[half]/
	\q[設定へ戻る,Menu_CONFIG]\n/
	\q[キャンセル,CANSEL]\e"
}

Menu_VANISH_TRUE {
	vanishflg = 1
	--
	"\1\s[10]\0\s[0]――そう。\e"
}

Menu_VANISH_FALSE {
	vanishflg = 0
	--
	"\1\s[10]\0\s[6]…\w4…\w4ふふ。\e"
}


//---- メニュー選択タイムアウト ---------------------------------------------------------
OnChoiceTimeout
{
	"\1\s[10]\0\s[0]時間切れ、\w4時には即決する力も必要よ?\e"
	"\1\s[10]\0\s[0]時間切れよ。\e"
}

//---- 報告
OnInputWebclap
{
	"\1\s[10]\0\s[6]\![execute,http-post,https://blankrune.sakura.ne.jp/patipati/index.cgi,--param-charset=Shift_JIS"
	--
	if reference[0] != '' {
		",--param=com=" + reference[0] + "（カッシーニの空隙）"
	}
	--
	",--async=patipati]"
	--
	"…\w4…。\w9\e"
}

//---- シェルごとの特殊メニュー
Menu_Shell
{
	case shellname {
		// 座り対応
		when "master", "Eternity Black", "拡大鏡", "White Lolita", "銀色", "瑠璃" {
			"%Menu_SitOrStand"
		}
		// Alternative - Cassis
		when "Alternative - Cassis" {
			"%Menu_AlternativeCassis"
		}
		// 月光浴
		when "月光浴" {
			"%Menu_MoonlightBlue"
		}
		// Bridal_Butterfly
		when "Bridal Butterfly", 'Bridal Butterfly 75%' {
			"%Menu_BridalButterfly"
		}
		// 月白
		when "月白" {
			"%Menu_Tukisiro"
		}
		// Gemini
		when "Gemini" {
			"%Menu_Gemini"
		}
		// 桜吹雪
		when "桜吹雪" {
			"%Menu_Sakurafubuki"
		}
		// 桃色月
		when "桃色月" {
			"%Menu_PinkMoon"
		}
		// ll29yo
		when "ll29yo" {
			"%Menu_Ll29yo"
		}
		// マジカル☆カシス 
		when "マジカル☆カシス" {
			"%Menu_Magicalcassis"
		}
	}	
}
