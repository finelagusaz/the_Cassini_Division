///////// 文　ゴーストテンプレート
///////// メニュー処理辞書
///////// written by umeici.

//**** メインメニューの展開 *************************************************************

//---- メインメニュー展開 ---------------------------------------------------------------
OpenMenu
{
	"\1\s[10]\0\s[0]"
	--
	/*
		シェルによってメニューを分岐させる。
		座り表示非対応シェルの場合は座り関係のメニューを表示しない。
	*/
	if strMenuMode != "new" {
		"\b[2]"
	}
	--
	"お品書き、ね。"
	"何?"
	"何かしら?"
	"ん?"
	"\s[6]ふふ。"
	"どうしたの?"
	"どうしたいの?"
	"?"
	"何がしたいの?"
	--
	"\_q"
	--
	AYATEMPLATE.MenuItem("トーク（t）","Menu_AITALK",200)
	--
	if ( STRLEN(AYATEMPLATE.LastTalk) > 0 ) {
		AYATEMPLATE.MenuItem("もう一度（l）","Menu_LASTTALK")
	}
	--
	if GetShellnameEx == "Gemini"
	{
		AYATEMPLATE.MenuItem("二人で話す","Menu_GEMINITALK")
	}
	--
	// クリスマスケーキ
	if flgGetCake == 1
	{
		AYATEMPLATE.MenuItem("ケーキを食べる","comChristmasCakes")
		--
		"\n[half]"
	}
	--
	AYATEMPLATE.MenuItem("コミュニケート（c）","Menu_COMMUNICATEOPEN")
	--
	if "オフィーリア" _in_ ghostexlist || "リーライナ" _in_ ghostexlist {
		AYATEMPLATE.MenuItem("他のゴーストに話しかける", "StartCommunicate")
	}
	--
	AYATEMPLATE.MenuItem("コマンド（d）", "Menu_COMMAND")
	--
	// 表示設定がnewでなければ従来通り表示
	if strMenuMode != "new"
	{
		Menu_Shell
	}
	--
	AYATEMPLATE.MenuItem("設定","Menu_CONFIG")
	--
	AYATEMPLATE.MenuItem("連絡","Menu_WEBCLAP")
	--
	"\n[half]"
	--
	AYATEMPLATE.MenuItem("メニューを閉じる","Menu_CANCEL")
}

// 桃色月
Menu_PinkMoon
{
	"【着せ替え】\n"
	--
	if PinkMoonSurfaceMode != 0 {
		"\![*]\q[スリットドレス,Menu_PmSlit]\n"
	}
	--
	if PinkMoonSurfaceMode != 1000 { 
		"\![*]\q[ワンピース,Menu_PmOp]\n"
	}
	--
	"\n[half]"
}

// ll29yo
Menu_Ll29yo
{
	"【着せ替え】\n"
	--
	if Ll29yoSurfaceMode != 0 {
		"\![*]\q[私服,Menu_Ll29yoNormal]\n"
	}
	--
	if Ll29yoSurfaceMode != 1000 {
		"\![*]\q[メイド服,Menu_Ll29yoMaid]\n"
	}
	--
	"\n[half]"
}

// マジカルカシス
Menu_Magicalcassis
{
	"【着せ替え】\n"
	--
	if MagicalCassisSurfaceMode != 0 {
		"\![*]\q[魔法少女,Menu_McMagical]\n"
	}
	--
	if MagicalCassisSurfaceMode != 1000 {
		"\![*]\q[和服,Menu_McJc]\n"
	}
	--
	if MagicalCassisSurfaceMode != 2000 {
		"\![*]\q[ちゃいな,Menu_McChina]\n"
	}
	--
	if MagicalCassisSurfaceMode != 3000 {
		"\![*]\q[セーラー服,Menu_McSailer]\n"
	}
	--
	if MagicalCassisSurfaceMode != 4000 {
		"\![*]\q[軍服風,Menu_McMu]\n"
	}
	--
	if MagicalCassisSurfaceMode != 5000 {
		"\![*]\q[ノースリーブ,Menu_McNosleeve]\n"
	}
	--
	"\n[half]"
}

// コマンド
Menu_COMMAND
{
	"\1\s[10]\0\s[0]\_q/
	\_a[Menu_CommandGreeting]「挨拶」\_a\n/
	\_a[Menu_CommandAction]「行動」\_a\n/
	\_a[Menu_CommandAsk]「質問」\_a\n/
	\_a[Menu_CommandOthers]「そのほか」\_a\n/
	\n[half]\n/
	\q[メニューへ戻る,openMenu]\n/
	\q[キャンセル,Menu_CANCEL]\e"
}

// 挨拶
Menu_CommandGreeting
{
	"\1\s[10]\0\s[0]\b[2]\_q/
	【挨拶】\n/
	\_a[comOhayo]「おはよう」\_a\n/
	\_a[comKonnichiha]「こんにちは」\_a\n/
	\_a[comKonbanha]「こんばんは」\_a\n/
	\_a[comBye]「いってきます」\_a\n/
	\_a[comTadaima]「ただいま」\_a\n/
	\_a[comOyasumi]「おやすみ」\_a\n"
	--
	if flgEat == 1
	{
		"\_a[comGochisosama]「ごちそうさま」\_a\n"
	}
	--
	"\n\n[half]\q[コマンドに戻る,Menu_COMMAND]\n/
	\q[キャンセル,Menu_CANCEL]\e"
}

// 行動
Menu_CommandAction
{
	"\1\s[10]\0\s[0]\b[2]\_q/
	【行動】\n/
	\_a[comFood]「何か食べたい」\_a\n/
	\_a[comTewonigiru]「手を握る」\_a\n/
	\_a[comDakishimeru]「抱きしめる」\_a\n/
	\_a[comLove]「愛をささやく」\_a\n/
	\_a[comCassis]「名前を呼ぶ」\_a\n/
	\_a[comArigato]「礼を言う」\_a\n/
	\_a[comGomen]「謝る」\_a\n"
	--
	if cntHead > likelimit
	{
		"\_a[comPockeyGame]「ポッキーゲーム」\_a\n"
	}
	--
	if Osuwari == 1 || Osuwari == 2
	{
		"\_a[comHizamakura]「膝枕」\_a\n"
	}
	--
	"\n\n[half]\q[コマンドに戻る,Menu_COMMAND]\n/
	\q[キャンセル,Menu_CANCEL]\e"
}

// 質問
Menu_CommandAsk
{
	"\1\s[10]\0\s[0]\b[2]\_q/
	【質問】\n/
	\_a[comAskFS]「FSについて」\_a\n/
	\_a[comAskMei]「あのヒトについて」\_a\n/
	\_a[comAskAge]「年齢について」\_a\n/
	\_a[comAskAndroid]「アンドロイドについて」\_a\n/
	\_a[comAskHobby]「趣味について」\_a\n/
	\_a[comAskLodger]「同居人について」\_a\n/
	\_a[comAskHagalaz]「あのバカについて」\_a\n/
	\_a[comAskThreeSize]「スリーサイズについて」\_a\n/
	"
	--
	if flgKnownHagalaz == 1
	{
		"\_a[comAskRelationHagalaz]「あのバカとの関係について」\_a\n"
	}
	--
	if !("%month" == 7 && "%day" == 14)
	{
		"\_a[comWhenBirthday]「誕生日を教えて」\_a\n"
	}

	--
	"\n\n[half]\q[コマンドに戻る,Menu_COMMAND]\n/
	\q[キャンセル,Menu_CANCEL]\e"	
}

// そのほか
Menu_CommandOthers
{
	"\1\s[10]\0\s[0]\b[2]\_q/
	【そのほか】\n/
	\_a[comNamaeOboete]名前を覚えて欲しい\_a\n/
	\_a[comStamp]スタンプをお願いする\_a\n/
	\_a[comKawaii]「かわいい」\_a\n/
	\_a[comTsundere]「ツンデレ」\_a\n/
	\_a[comTereya]「照れ屋だね」\_a\n/
	\_a[comNadete]「撫でて」\_a\n/
	\_a[comOenshite]「応援して」\_a\n/
	\_a[comKekon]「結婚してくれ」\_a\n/
	\_a[comTwitter]「Twitter」\_a\n"
	--
	if flgComOyasumiOther == 1
	{
		"\_a[resComOyasumiOther]「一緒に寝よう。」\_a\n"
	}
	--
	if "%month" == 7 && "%day" == 14
	{
		"\_a[comBirthday]「誕生日おめでとう」\_a\n"
	}
	--
	if cntEros > 10 && shellname == "月白" && TukisiroSurfaceMode != 3000
	{
		"\_a[Menu_TukisiroTuckup]「下着見せて」\_a\n"
	}
	elseif shellname == "月白" && TukisiroSurfaceMode == 3000 {
		"\_a[Menu_TukisiroTuckdown]「満足した」\_a\n"
	}
	elseif shellname == "Gemini" && flgLove == 1
	{
		if GeminiSurfaceMode >= 0 && GeminiSurfaceMode <= 2000
		{
			"\_a[Menu_GeminiTuckup]「下着見せて」\_a\n"
		}
		else
		{
			"\_a[Menu_GeminiDress]「服を着て」\_a\n"
		}
	}
	--
	AYATEMPLATE.MenuItem("コマンドに戻る","Menu_COMMAND")
	--
	AYATEMPLATE.MenuItem("キャンセル","Menu_CANCEL",150)
}

// コミュニケート
Menu_COMMUNICATEOPEN
{
	//---- コミュニケートボックスオープン
	"\1\s[10]\0\s[0]何かしら?"
	"\1\s[10]\0\s[0]はいはい。"
	"\1\s[10]\0\s[0]ん?"
	--
	"\![open,communicatebox]\e"
}

//**** メニュー項目選択 *****************************************************************

//---- メインメニュー
Menu_AITALK
{
	//---- 明示的にAIトーク開始を指示
	OnAiTalk
}

Menu_GEMINITALK {
	_talk_array = IARRAY
	_talk_array ,= aryShellGeminiTalk

	if flgLove == 1 {
		_talk_array ,= aryShellGeminiLoveTalk
	}
	
	parallel _talk_array
}

TALKINTERVAL {
	//---- トーク頻度の調整
	"\1\s[10]\0\s[0]どのくらいがいいの?\n\n/
	\q[うるさいくらい,TALKMAX]\n/
	\q[ふつう,TALKNORMAL]\n/
	\q[静か,TALKPOOR]\n/
	\q[黙ってて,TALKNONE]\e"
}



//------------------------------
//桃色月
//------------------------------
// スリットドレス
Menu_PmSlit
{
	SurfaceModeChangeEx(0, 0);
	--
	"\0\s[0]ん?\e"
}

// ワンピース
Menu_PmOp
{
	SurfaceModeChangeEx(1000, 0);
	--
	"\0\s[0]そう。\e"
}

//------------------------------
// ll29yo
//------------------------------
// 私服
Menu_Ll29yoNormal
{
	SurfaceModeChangeEx(0, 0);
	--
	"\0\s[0]着替えてくるわ。\e"
}

// メイド
Menu_Ll29yoMaid
{
	SurfaceModeChangeEx(1000, 0);
	--
	"\0\s[0]これでいいのかしら、\w4ご主人様。\w9\n/
	\s[5]なんて、ね。\e"
}

//------------------------------
// マジカル☆カシス
//------------------------------
// 魔法少女
Menu_McMagical
{
	SurfaceModeChangeEx(0, 0);
	--
	"\0\s[6]少し恥ずかしいのだけど……。\e"
}

// 和服
Menu_McJc
{
	SurfaceModeChangeEx(1000, 0);
	--
	"\0\s[0]これは、\w4どう?\e"
}

// ちゃいな
Menu_McChina
{
	SurfaceModeChangeEx(2000, 0);
	--
	"\0\s[0]こういうのはどうかしら?\e"
}

// セーラー服
Menu_McSailer
{
	SurfaceModeChangeEx(3000, 0);
	--
	"\0\s[8]はやくしないと、\w4遅れるわよ。\w9\n/
	\s[5]なんてね。\e"
}

// 軍服
Menu_McMu
{
	SurfaceModeChangeEx(4000, 0);
	--
	"\0\s[5]どう、\w4似合う?\e"
}

// ノースリーブ
Menu_McNosleeve
{
	SurfaceModeChangeEx(5000, 0);
	--
	"\0\s[5]どうかしら?\e"
}

//------------------------------------------------------------------------------

// Web拍手
Menu_WEBCLAP {
	"\1\s[10]\0\s[0]?\w9\n/
	\![open,inputbox,OnInputWebclap,0,バグ報告などどうぞ（最大200字）]\e"
}
Menu_CANCEL {
	//---- キャンセルされた
	"\1\s[10]\0\s[6]そう。\e"
}

//---- トーク頻度
Menu_TALKMAX {
	//---- うるさく話す(15秒間隔)
	aitalkinterval = 15
	"\1\s[10]\0\s[0]気に障るようなら言ってちょうだい。\e"
}

Menu_TALKNORMAL {
	//---- ふつうに話す(3分間隔)
	aitalkinterval = 180
	"\1\s[10]\0\s[0]普通というのも曖昧ね。\e"
}

Menu_TALKPOOR {
	//---- 静かに話す(5分間隔)
	aitalkinterval = 300
	"\1\s[10]\0\s[0]静か、ね。\e"
}

Menu_TALKNONE {
	//---- 話さない
	aitalkinterval = 0
	"\1\s[10]\0\s[0]わかったわ。\w9\n本でも読んでようかしら。\e"
	"\1\s[10]\0\s[6]そう、\w4静かにしているわ。\e"
}

//---- ヘッドライン
Menu_HEADLINECANCEL {
	//---- キャンセルされた
	"\1\s[10]\0\s[0]そう。\e"
}

//---- トークウェイト設定
Menu_WCC {
	"\1\s[10]\0\s[0]\q[早く,WCFAST]\n/
	\q[普通,WCNORMAL]\n/
	\q[遅く,WCSLOW]\e"
}

Menu_WCFAST {
	WC = "fast"
	"\1\s[10]\0\s[6]噛まない程度に早くするわ。\w9\e"
}

Menu_WCNORMAL {
	WC = "normal"
	"\1\s[10]\0\s[6]そう。\w9\e"
}

Menu_WCSLOW {
	WC = "slow"
	"\1\s[10]\0\s[6]わかったわ。\w9\e"
}

//---- 設定
Menu_CONFIG {
	if strMenuMode == "new" {
		"\b[2]"
	}
	--
	"\1\s[10]\0\s[0]どうしたいの?\n\n/
	\![*]\q[トーク頻度,TALKINTERVAL]\n/
	\![*]\q[トーク速度,WCC]\n"
	--
	if shellname == "master" {
		if flgTooltip != "1" {
			"\![*]\q[ツールチップを表示する,Menu_TooltipEnable]\n"
		}
		else {
			"\![*]\q[ツールチップを表示しない,Menu_TooltipDisable]\n"
		}
	}
	--
	if strMenuMode == "new" && flgNoSfcnv != 1
	{
		Menu_Shell
	}
	--
	if flgNoSfcnv != 1
	{
		if strMenuMode == "new" {
			"\![*]\q[着せ替え・座る／立つメニューをメニューに,Menu_Classic]\n"
		}
		else {
			"\![*]\q[着せ替え・座る／立つメニューを設定に,Menu_New]\n"
		}
	}
	--
	"\![*]\q[アンインストール設定,Menu_VANISHCONTROL]\n"
	--
	// 見切れ設定
	if flgMikireEnable == "1" {
		"\![*]\q[見切れトークを無効にする,Menu_MikireDisable]\n"
	}
	else {
		"\![*]\q[見切れトークを有効にする,Menu_MikireEnable]\n"
	}
	--
	"[half]"
	--
	"\n\n[half]\q[メニューへ戻る,openMenu]\n/
	\q[キャンセル,CANCEL]\e"
}

// ツールチップ制御
Menu_TooltipEnable {
	flgTooltip = 1
	"\1\s[10]\0\s[6]わかったわ。\e"
}

Menu_TooltipDisable {
	flgTooltip = 0
	"\1\s[10]\0\s[6]そう。\e"
}

// 見切れトーク制御
Menu_MikireEnable {
	flgMikireEnable = "1"
	"\1\s[10]\0\s[6]\e"
}

Menu_MikireDisable {
	flgMikireEnable = "0"
	"\1\s[10]\0\s[0]\e"
}


// メニュー設定
Menu_Classic {
	strMenuMode = "classic"
	"\1\s[10]\0\s[0]こうかしら?\e"
}

Menu_New {
	strMenuMode = "new"
	"\1\s[10]\0\s[0]こう?\e"
}

// アンインストールメニュー設定
Menu_VANISHCONTROL {
	"（アンインストールメニューの表示設定です。）\n/
	（現在:"
	--
	if vanishflg != 1 {
		"非表示)\n/
		\![*]\q[アンインストールメニュー表示,Menu_VANISH_TRUE]"
	} else {
		"表示)\n/
		\![*]\q[アンインストールメニュー非表示,Menu_VANISH_FALSE]"
	}
	--
	"\n\n[half]/
	\q[設定へ戻る,Menu_CONFIG]\n/
	\q[キャンセル,CANSEL]\e"
}

Menu_VANISH_TRUE {
	vanishflg = 1
	--
	"\1\s[10]\0\s[0]――そう。\e"
}

Menu_VANISH_FALSE {
	vanishflg = 0
	--
	"\1\s[10]\0\s[6]…\w4…\w4ふふ。\e"
}


//---- メニュー選択タイムアウト ---------------------------------------------------------
OnChoiceTimeout
{
	"\1\s[10]\0\s[0]時間切れ、\w4時には即決する力も必要よ?\e"
	"\1\s[10]\0\s[0]時間切れよ。\e"
}

//---- 報告
OnInputWebclap
{
	"\1\s[10]\0\s[6]\![execute,http-post,https://blankrune.sakura.ne.jp/patipati/index.cgi,--param-charset=Shift_JIS"
	--
	if reference[0] != '' {
		",--param=com=" + reference[0] + "（カッシーニの空隙）"
	}
	--
	",--async=patipati]"
	--
	"…\w4…。\w9\e"
}

//---- シェルごとの特殊メニュー
Menu_Shell
{
	case shellname {
		// 座り対応
		when "master", "Eternity Black", "拡大鏡", "White Lolita", "銀色", "瑠璃" {
			"%Menu_SitOrStand"
		}
		// Alternative - Cassis
		when "Alternative - Cassis" {
			"%Menu_AlternativeCassis"
		}
		// 月光浴
		when "月光浴" {
			"%Menu_MoonlightBlue"
		}
		// Bridal_Butterfly
		when "Bridal Butterfly", 'Bridal Butterfly 75%' {
			"%Menu_BridalButterfly"
		}
		// 月白
		when "月白" {
			"%Menu_Tukisiro"
		}
		// Gemini
		when "Gemini" {
			"%Menu_Gemini"
		}
		// 桜吹雪
		when "桜吹雪" {
			"%Menu_Sakurafubuki"
		}
		// 桃色月
		when "桃色月" {
			"%Menu_PinkMoon"
		}
		// ll29yo
		when "ll29yo" {
			"%Menu_Ll29yo"
		}
		// マジカル☆カシス 
		when "マジカル☆カシス" {
			"%Menu_Magicalcassis"
		}
	}	
}

// ------------------------
// Gemini手つなぎ用
// ------------------------
// 手をつなぐ
Menu_GeminiHandshake
{
	SetFit
	HandshakeFlg = 1
}

// 手をはなす
Menu_GeminiBreak
{
	HandshakeFlg = 0
}

//---- さっきの話は
Menu_LASTTALK
{
	AYATEMPLATE.LastTalk
}
