///////// マウス入力関連辞書（コア）

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************
OnMouseMove
{
	AYATEMPLATE.MouseMoveWheelFunc(0,50, "%(EMGetPrefix)Move")
}

OnMouseWheel
{
	_result = ''

	if TOINT(reference[2]) > 0 {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4, "%(EMGetPrefix)WheelUp")
	}
	else {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4, "%(EMGetPrefix)WheelDown")
	}
	
	if _result == '' {
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4, "%(EMGetPrefix)Wheel")
	}
	
	_result
}

OnMouseDoubleClick
{
	AYATEMPLATE.MouseEventExec("%(EMGetPrefix)DoubleClick");
}

EMMouseHitExec
{
	_name = _argv[0]
	if ISFUNC(_name) {
		_text = EVAL(_name)
		if STRLEN(_text) != 0 {
			_text
			return
		}
	}

	if RE_MATCH(_name,"(.+)[0-9]+") != 0 {
		_name = RE_GETSTR[1]
		if ISFUNC(_name) {
			_text = EVAL(_name)
			if STRLEN(_text) != 0 {
				_text
				return
			}
		}
	}
}

// FIXME: シェルの判定を共通処理にする
EMGetPrefix
{
	if sender == 'The Hand' {
		'TheHandMouse'
	}
	elseif shellname == 'Gemini' {
		case GeminiSurfaceMode
		{
			when 1000, 4000 {
				'CastorMouse'
			}
			when 2000, 5000 {
				'GeminiMouse'
			}
			others {
				'Mouse'
			}
		}
	}
	elseif shellname == '桜吹雪'
	{
		'SakurafubukiMouse'
	}
	else {
		'Mouse'
	}
}

// ホイールクリック
OnMouseClickEx
{
	_prefix = EMGetPrefix
	_name = "%(_prefix)ClickEx%(reference[3])%(reference[4])"

	_script = EMMouseHitExec(_name)
	if STRLEN(_script) != 0 {
		_script
		return
	}

	EVAL("%(_prefix)ClickEx%(reference[3])")
}