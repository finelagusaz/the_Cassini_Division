///////// マウス入力関連辞書（コア）

//******************************************************************************
//マウス処理部コア
//AYATEMPLATE.MouseMoveWheelFuncの2番目の数字が「感度」です。
//1番目の数字（0か1）は、MoveかWheelかを判別するためのもので、
//変更の必要はありません。
//******************************************************************************
OnMouseMove
{
	AYATEMPLATE.MouseMoveWheelFunc(0,50, "%(EMGetPrefix)DoubleClick")
}

OnMouseWheel
{
	_result = ''

	if TOINT(reference[2]) > 0 {
		_name = "%(EMGetPrefix)WheelUp"
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4, _name)
	}
	else {
		_name = "%(EMGetPrefix)WheelDown"
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4, _name)
	}
	
	if _result == '' {
		_name = "%(EMGetPrefix)Wheel"
		_result = AYATEMPLATE.MouseMoveWheelFunc(1,4, _name)
	}
	
	_result
}

OnMouseDoubleClick
{
	AYATEMPLATE.MouseEventExec("%(EMGetPrefix)DoubleClick");
}

EMMouseHitExec
{
	_name = _argv[0]
	if ISFUNC(_name) {
		_text = EVAL(_name)
		if STRLEN(_text) != 0 {
			_text
			return
		}
	}

	if RE_MATCH(_name,"(.+)[0-9]+") != 0 {
		_name = RE_GETSTR[1]
		if ISFUNC(_name) {
			_text = EVAL(_name)
			if STRLEN(_text) != 0 {
				_text
				return
			}
		}
	}
}

EMGetPrefix
{
	if sender == 'The Hand' {
		'MouseTH'
	}
	elseif shellname == 'Gemini' {
		case GeminiSurfaceMode
		{
			when 1000, 4000 {
				'MouseGEC'
			}
			when 2000, 5000 {
				'MouseGEN'
			}
			others {
				'Mouse'
			}
		}
	}
	elseif shellname == '桜吹雪'
	{
		'MouseSAK'
	}
	else {
		'Mouse'
	}
}

// ホイールクリック
OnMouseClickEx
{
	_prefix = EMGetPrefix
	_name = "%(_prefix)_ClickEx%(reference[3])%(reference[4])"

	_script = EMMouseHitExec(_name)
	if STRLEN(_script) != 0 {
		_script
		return
	}

	EVAL("%(_prefix)_ClickEx%(reference[3])")
}